plugins {
    id 'org.cadixdev.licenser' version '0.6.1' apply false
    id 'com.gradleup.shadow' version '8.3.8' apply false
}

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

allprojects {
    group = 'me.lucko'
    version = '1.10-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'maven-publish'

    ext {
        baseVersion = '1.10'
        patchVersion = determinePatchVersion()
        pluginVersion = baseVersion + '.' + patchVersion
        pluginDescription = 'spark is a performance profiling plugin/mod for Minecraft clients, servers and proxies.'

        applyExcludes = { Jar jarTask ->
            jarTask.exclude 'module-info.class'
            jarTask.exclude 'META-INF/maven/**'
            jarTask.exclude 'META-INF/proguard/**'
            jarTask.exclude 'META-INF/LICENSE'
            jarTask.exclude 'META-INF/NOTICE'
            jarTask.exclude '**/*.proto'
            jarTask.exclude '**/*.proto.bin'
            jarTask.exclude 'linux-arm64/**'
            jarTask.exclude 'linux-x64/**'
            jarTask.exclude 'macos/**'
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events = [TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED]
            exceptionFormat = TestExceptionFormat.FULL
            showExceptions = true
            showCauses = true
            showStackTraces = true
        }
    }

    processResources {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }

    license {
        header = rootProject.file('HEADER.txt')
        include '**/*.java'
    }

    repositories {
        maven { url "https://libraries.minecraft.net" }
        mavenCentral()
        maven { url "https://repo.lucko.me/" }
        maven { url "https://jitpack.io" }
    }

    afterEvaluate { project ->
        if (project.plugins.hasPlugin('java')) {
            project.publishing {
                publications {
                    mavenJava(MavenPublication) {
                        from components.java
                        pom {
                            name = 'spark'
                            description = 'spark is a performance profiling plugin/mod for Minecraft clients, servers and proxies.'
                            url = 'https://spark.lucko.me/'
                        }
                    }
                }
            }
        }
    }
}

def determinePatchVersion() {
    return '0'
}
